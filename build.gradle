apply plugin: 'java'
apply plugin: 'maven'

group='com.codeborne'
archivesBaseName = 'selenide'
version='1.4'

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
sourceCompatibility = 1.5
targetCompatibility = 1.5

defaultTasks 'clean', 'libs', 'test', 'install'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.seleniumhq.selenium:selenium-java:2.23.1'
  compile 'junit:junit:4.10'
  compile 'org.hamcrest:hamcrest-all:1.1'
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.runtime
  into "$buildDir/lib"
}

test {
  exclude 'com/codeborne/selenide/integrationtests/**'
  testLogging.showStandardStreams = true
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.0-rc-1'
  jarFile = './gradle-wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}

