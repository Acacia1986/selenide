apply plugin: 'java'
apply plugin: 'maven'

group='com.codeborne'
archivesBaseName = 'selenide'
version='1.10-SNAPSHOT'

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
sourceCompatibility = 1.5
targetCompatibility = 1.5

defaultTasks 'clean', 'libs', 'test', 'install'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.seleniumhq.selenium:selenium-java:2.28.0'
  compile 'junit:junit:4.11'
  compile fileTree(dir: 'lib', include: '*.jar')
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.mockito:mockito-all:1.9.5'
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.runtime
  from configurations.testCompile
  into "$buildDir/lib"
}

test {
  exclude 'com/codeborne/selenide/integrationtests/**'
  systemProperties['browser'] = 'htmlunit'
  // systemProperties['browser'] = 'chrome'
  // systemProperties['webdriver.chrome.driver'] = '/usr/bin/chromedriver'

  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.3'
  jarFile = './gradle-wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}

