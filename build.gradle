apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.5
version = '1.0-SNAPSHOT'
description = "Selenide is an addition for Selenium WebDriver to facilitate writing JUnit tests with Selenium"

jar {
  manifest {
    attributes 'Implementation-Title': 'Selenide', 'Implementation-Version': version, 'Author': 'Andrei Solntsev', 'URL': 'https://github.com/asolntsev/selenide'
  }
}

repositories {
  mavenCentral()
}

configure(install.repositories.mavenInstaller) {
  pom.groupId = 'com.codeborne'
  pom.artifactId = 'selenide'
  pom.version = '1.0-SNAPSHOT'
}

dependencies {
  compile 'org.seleniumhq.selenium:selenium-java:2.15.0'
  compile 'org.mortbay.jetty:jetty:6.1.26'
  runtime 'jetty:javax.servlet:5.1.12'
  runtime 'org.mortbay.jetty:jsp-2.1:6.1.14'
  compile 'junit:junit:4.9'
  compile 'org.hamcrest:hamcrest-all:1.1'
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.runtime
  into "$buildDir/lib"
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

test {
  systemProperties 'file.encoding': 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

defaultTasks 'clean', 'libs', 'test', 'jar', 'install'

